apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "net.wagsn.note"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode getVersion("code")
        description "本次版本更新了：\r\n【文本编辑】修复内容编辑框背景颜色。"
        versionName getVersion("name")
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
//            shrinkResources true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// GreenDao Build 脚本
greendao {
    schemaVersion 1 // 数据库版本
    targetGenDir 'src/main/java'
    // dao 生成包
    daoPackage "${android.defaultConfig.applicationId}.storage.db"
}

// 主版本号.子版本号.修正版本号 Major.Minor[.Patch[.Build[-Suffix]]] 语义化版本号：https://semver.org/lang/zh-CN/
// [主版本号].[子版本号].[阶段/修复版本号].[日期/编译版本号]-[希腊字母版本号]，希腊字母版本号共有5种，分别为：base、alpha、beta、rc、release。例如：1.1.1.051021_beta
def getVersion(type){
    int major = 0
    int minor = 1
    int patch = 7
    int build = Integer.parseInt(new Date().format("yyMMdd", TimeZone.getTimeZone("GMT+08:00")))
    if("code" == type){
        int code = major
        code = (int)Math.pow(10, minor.toString().length()+1)*code+minor
        code = (int)Math.pow(10, patch.toString().length()+1)*code+patch
        code = (int)Math.pow(10, build.toString().length()+1)*code+build
        return code
    }else {
        return "${major.toString()}.${minor.toString()}.${patch.toString()}.${build.toString()}-base"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.zhy:okhttputils:2.6.2'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'org.greenrobot:eventbus:3.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
